/*
 * Copyright (c) 2016-2018 Projekt Substratum
 * This file is part of Substratum.
 *
 * SPDX-License-Identifier: GPL-3.0-Or-Later
 */

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'jacoco'

jacoco {
    toolVersion = "$jacocoVersion"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

final def gitHash = { ->
    final def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    stdout.toString().trim()
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
final def keystorePropertiesFile = rootProject.file("keystore.properties")

android {
    compileSdkVersion 28
    lintOptions {
        abortOnError false
        checkAllWarnings false
        checkReleaseBuilds false
        ignoreWarnings true
        quiet true
    }
    dataBinding.enabled true
    android.applicationVariants.all { final variant ->
        variant.outputs.all {
            outputFileName = "substratum_${variant.versionCode}_${gitHash()}.apk"
        }
    }
    defaultConfig {
        applicationId "projekt.substratum"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 1001
        versionName "one thousand one"
        buildConfigField "java.util.Date", "buildTime", "new java.util.Date(" + System.currentTimeMillis() + "L)"
        buildConfigField "String", "GIT_HASH", "\"${gitHash()}\""
        buildConfigField "boolean", "ENHANCED_LOGGING", "false"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    // If the keystore file exists
    if (keystorePropertiesFile.exists()) {
        // Initialize a new Properties() object called keystoreProperties.
        final def keystoreProperties = new Properties()

        // Load your keystore.properties file into the keystoreProperties object.
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            useProguard false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        javaMaxHeapSize '2048m'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }
    }
}

ext {
    databindingVersion = '3.3.0-alpha08'
    androidXVersion = '1.0.0-rc02'
    firebaseVersion = '16.0.3'
    crashlyticsVersion = '2.9.5'
    glideVersion = '4.8.0'
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ])
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Android Support Libraries
    implementation "androidx.databinding:databinding-runtime:$databindingVersion"
    implementation "androidx.appcompat:appcompat:$androidXVersion"
    implementation "androidx.exifinterface:exifinterface:$androidXVersion"
    implementation "androidx.cardview:cardview:$androidXVersion"
    implementation "androidx.recyclerview:recyclerview:$androidXVersion"
    implementation "androidx.legacy:legacy-support-v13:$androidXVersion"
    implementation "com.google.android.material:material:1.0.0-rc01"
    implementation "androidx.palette:palette:$androidXVersion"
    implementation "androidx.legacy:legacy-preference-v14:$androidXVersion"

    // Firebase
    implementation "com.google.firebase:firebase-core:$firebaseVersion"
    implementation "com.google.firebase:firebase-database:16.0.1"

    // Crashlytics
    implementation "com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion"

    // Image Downloading and Caching
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"

    // Commons IO
    //noinspection GradleDependency
    implementation 'commons-io:commons-io:2.5'

    // APK Signer
    implementation 'com.android.tools.build:apksig:3.1.4'

    // App Intro
    implementation 'com.stephentuso:welcome:1.4.1'

    // Floating Action Buttons
    implementation 'com.gordonwong:material-sheet-fab:1.2.1'

    // About Libraries
    implementation('com.mikepenz:aboutlibraries:6.0.9@aar') {
        transitive = true
    }

    // Gesture RecyclerView
    implementation 'com.thesurix.gesturerecycler:gesture-recycler:1.5.0'

    // ZT Zip
    //noinspection GradleDependency
    implementation 'org.zeroturnaround:zt-zip:1.13'

    // Image Processing
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.7.0'

    // Expandable Layout
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'

    // Floating App Head
    implementation 'com.github.recruit-lifestyle:FloatingView:2.3.2'

    // Crash Activity
    implementation 'cat.ereza:customactivityoncrash:2.2.0'

    // Splash screen svg animation
    implementation 'com.jaredrummler:animated-svg-view:1.0.5'

    // Code coverage tests
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestUtil 'com.android.support.test:orchestrator:1.0.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.7.1'
}
apply plugin: 'com.google.gms.google-services'